set dotenv-load

# Lists all available backend commands.
_default:
  @just --list

version := `git describe --tags --always --dirty 2>/dev/null || echo dev`

# Builds the gastrolog binary.
build:
  go build -ldflags "-X main.version={{version}}" -o gastrolog ./cmd/gastrolog

# Runs the gastrolog backend with raft config (default).
run-raft:
  go run ./cmd/gastrolog server --home data

# Runs the gastrolog backend with in-memory config (no persistence).
run-memory:
  go run ./cmd/gastrolog server --config-type memory

alias run := run-raft

# Runs the gastrolog backend with full bootstrap (memory store + chatterbox).
run-bootstrap:
  go run ./cmd/gastrolog server --bootstrap --home data

# Runs the gastrolog backend with authentication disabled (all requests treated as admin).
run-noauth:
  go run ./cmd/gastrolog server --no-auth --home data

# Runs the gastrolog backend with pprof server.
pprof:
  go run ./cmd/gastrolog server --home data --pprof localhost:6060

# Runs a config CLI command against the local server. Usage: just cfg filter list
cfg *args:
  go run ./cmd/gastrolog --home data config {{args}}

# Cross-compiles for a specific OS/arch. Usage: just build-cross linux amd64
build-cross os arch:
  CGO_ENABLED=0 GOOS={{os}} GOARCH={{arch}} go build -ldflags "-X main.version={{version}}" -o gastrolog-{{os}}-{{arch}} ./cmd/gastrolog

# Builds release binaries for all supported platforms.
build-all:
  just build-cross linux amd64
  just build-cross linux arm64
  just build-cross darwin amd64
  just build-cross darwin arm64

# Lints Go code (cognitive complexity, cyclomatic complexity, gocritic).
lint:
  golangci-lint run ./...

# Embeds built frontend assets into the backend (brotli-compressed).
embed-frontend:
  rm -rf internal/frontend/dist/*
  cp -r ../frontend/dist/* internal/frontend/dist/
  go run ./cmd/compress-assets internal/frontend/dist
